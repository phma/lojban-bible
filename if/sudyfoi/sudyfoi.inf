!sudyfoi.if

Replace Banner;
Include "Parser";
Include "VerbLib";

! Classes

Class Item;
Class Room has light;
Class Scene has scenery;

! Starting supplies for the player

Item djacu_vasru "djacu ke skapi vasru" with description
	".i lei skapi cu terzba le ka'e vasru be loi litki",
	name 'skapi' 'vasru', article "le",
	before [;
	Receive:
		".i le vasru loi litki po'o ka'e bevri";
	Empty:
		if (child(self)) {
			remove(child(self));
			".i do kuntygau le vasru .i lei litki cu cliva ra";
		}
	],
	has container open;
Item -> djacu_in_vasru "djacu" with description
	".i lei djacu cu blanu",
	before [;
	Drink:
		! No warning; but they shouldn't waste it.
		remove self;
		".i do pinxe lei djacu .i ri pluka";
	Take:
		<<Empty djacu_vasru>>;
	],
	name 'djacu', article "lei";
Item litru_sanmi "litru sanmi" with description
	".i lei cidja cu vrusi xlali je rigni gi'eku'i pu'i xagji mansa",
	warned_about_eat false,
	name 'sanmi' 'cidja', article "le",
	before [;
	Eat:
		! Warn them not to waste it, but let them waste it
		! anyway if they really want to.
		if (self.warned_about_eat == false) {
			self.warned_about_eat = true;
			".i .e'u do bilga lenu ralte lei cidja pu'o
			lonu do ca'a nitcu ri";
		}
	],
	after [;
	Eat:
		remove self;
		".i do citka lei cidja .i ri xlali vrusi mutce";
	],
	has edible;

Item cartu "cartu be le suyfoi" with
	description [;
		print ".i le cartu cu pixra^^";
		style fixed; print "\
+----------------------------+^
|               sudyfoi      |^
| ###                        |^
| #$# le cirko tcadu no'u    |^
| #=#   la ternon.           |^
|  .                         |^
|  .       barda manti gugde |^
|  ..                        |^
|{ {.{ { { { { { { { { {     |^
|   ..                 { { { |^
|    .........X ckape      { |^
|             .              |^
|   be'a      ......         |^
|vu'a + du'a       .         |^
|   ne'u           .         |^
+----------------------------+^";
		style roman;
		rtrue;
	],
	name 'cartu', article "le";

! Rooms and such

Room krasi_dargu ".i vi krasi le dargu fi le tcadu" with description
	".i le dargu cu tcena le jvinu to'o do .i lei cmalu tricu cu panra
	le go'i noi se cpana lei srasu poi pagre lei lolro'iboi .i le tsani
	ca milxe manku ca'o lenu le solri cu canci",
	n_to pluta_le_sudyfoi,
	name 'krasi';

Scene "dargu" with name 'dargu' 'srasu', description
	".i lei dargu cu puze'u to'e se cikre gi'e se gunta lei bunre srasu",
	found_in krasi_dargu;
Scene "tricu" with name 'tricu', description
	".i lei tricu cu bunre gi'e jorne no pezli .i lakne fa leza'i ty.
	na se djacygau ze'uku",
	found_in krasi_dargu;

Room pluta_le_sudyfoi ".i vi pluta le sudyfoi" with description
	".i do ca'o litru le pluta be le sudyfoi .i lei cmalu tricu cu
	naca panra .ije lei lolro'iboi cu jai se sisti",
	n_to sudyfoi_korbi,
	s_to krasi_dargu,
	name 'pluta';

Scene "tsani" with name 'tsani' 'gapru', description
	".i lei tsani cu xekbla gi'e se skarygau le solri noi caca'o canci",
	found_in krasi_dargu pluta_le_sudyfoi;

Room sudyfoi_korbi ".i vi korbi be le sudyfoi" with description
	".i le sudyfoi cu vi berti .i le sinxa cu sanli vi",
	s_to pluta_le_sudyfoi,
	name 'korbi';

Scene "sinxa" with name 'sinxa', description
	".i le sinxa cu xusra la'elu ko se kajde vi li'u",
	found_in sudyfoi_korbi;

Scene "solri" with name 'solri', description
	".i lenu viska cu rinka leza'i do kanla cu cortu",
	found_in krasi_dargu pluta_le_sudyfoi sudyfoi_korbi;

! Entry point

[ Initialise;
	location = krasi_dargu;
	move djacu_vasru to player;
	move litru_sanmi to player;
	move cartu to player;
];

[ Banner;
	print "ni'o darsyli'u tu'a le sudyfoi .i le lisri se finti
		la .djordan. .i ki'e la camgusmis. noi finti la .inform.^";
	print ".i lezu'o darsyli'u cu cfari vau .o'idai .i ";

	! A little hint about the future.
	style bold;
	print "ko rivbi loi barda manti^^";
	style roman;

	! Explaination of the story so far
	print "ni'o do virnu je darsi litru .i puzuku do zatfa'i le cartu
		be le sugyfoi .i cy. xusra ledu'u le cirko tcadu cu zvati
		le condi be fi loi tcadu kujoi loi prenu be'o sudyfoi .iji'a
		ti'e le cirko tcadu goi la ternon. cu vasru lo barda ni
		jemna je solji^^";
	print "ni'o do ba'o klama le tcadu noi snanu traji be fo lo'i
		tcadu be vi le sudyfoi .i do tavla lei diklo prenu .ije
		no le se go'i cu djuno lo vajni le do nu darsyli'u .i ba'o
		lenu cpacu lei cidja .e lei djacu kei do cfarygau lenu
		be'a litru^";
];

! Grab rlp's lojban grammar stuffs
Include "LojbanG";
